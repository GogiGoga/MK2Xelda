
##################################
# Level 9	INSIDE HOUSES
# HERE you receive all of your quests
##################################
INC_DECORATIONS decorations9.spt

#actors
#	$TALK_ISLAND_KEEPER 2
#	$TALK_GATE_KEEPER 3
#	$TALK_DUNGEON_GATEMASTER 4
#	$TALK_WOODSMAN 5
#	$TALK_BLACKSMITH 6
#	$TALK_ISLAND_KEEPER_DAUGHTER 7
#	$TALK_MOTHER_DUNGEON_GATEMASTER 8
#	$TALK_GATEKEEPER 9
#	$TALK_HUNGRY_MAGICIAN 9
#	$TALK_MAGICIAN_COUSIN 10
#	$TALK_DESERT_GATEMASTER 11
#	$TALK_DEMOLITION_EXPERT 12

ENTERING GAME
	IF TRUE
		#from level 0 Island Keeper
		IF $WARPED_TO_HERE = 75
		THEN
			SET $WARPED_TO_HERE = 0
			WARP_TO 1, 7, 8
			BREAK
		END
		
		#from level 0 child gatekeeper
		IF $WARPED_TO_HERE = 77
		THEN
			SET $WARPED_TO_HERE = 0
			WARP_TO 0, 7, 8
			BREAK
		END
		
		#woodsman
		IF $WARPED_TO_HERE = 79
		THEN
			SET $WARPED_TO_HERE = 0
			WARP_TO 2, 7, 8
			BREAK
		END
		
		#caveman
		IF $WARPED_TO_HERE = 81
		THEN
			SET $WARPED_TO_HERE = 0
			WARP_TO 3, 7, 8
			BREAK
		END
		
		#BLACKSMITH
		IF $WARPED_TO_HERE = 83
		THEN
			SET $WARPED_TO_HERE = 0
			WARP_TO 4, 7, 8
			BREAK
		END
		
		#HUNGRY MAGICIAN
		IF $WARPED_TO_HERE = 85
		THEN
			SET $WARPED_TO_HERE = 0
			WARP_TO 5, 7, 8
			BREAK
		END
		
		#MAGICIAN BROTHER
		IF $WARPED_TO_HERE = 87
		THEN
			SET $WARPED_TO_HERE = 0
			WARP_TO 6, 7, 8
			BREAK
		END
		
		#DESERT GATEMASTER
		IF $WARPED_TO_HERE = 89
		THEN
			SET $WARPED_TO_HERE = 0
			WARP_TO 7, 7, 8
			BREAK
		END
		
		#DESERT GATEMASTER
		IF $WARPED_TO_HERE = 91
		THEN
			SET $WARPED_TO_HERE = 0
			WARP_TO 8, 7, 8
			BREAK
		END
		
		#WIZARD
		IF $WARPED_TO_HERE = 93
		THEN
			SET $WARPED_TO_HERE = 0
			WARP_TO 9, 7, 8
			BREAK
		END
		
		#$TALK_DEMOLITION_EXPERT
		IF $WARPED_TO_HERE = 95
		THEN
			SET $WARPED_TO_HERE = 0
			WARP_TO 10, 7, 8
			BREAK
		END
		
		IF $WARPED_TO_HERE = 99
		THEN
			SET $ENEMS_KILLED = 0
			SET $SWITCH_8 = 0
			SET $SWITCH_SPIKES = 0
			
			SET $WARPED_TO_HERE = 0			
			WARP_TO 12, 13, 8
			BREAK
		END	

END

#CHILD GATEKEEPER
ENTERING SCREEN 0
	IF TRUE
	THEN
	
		SET_FIRE_ZONE 97, 129, 127, 159
		
		#this is obsolete, leaving it in for now
		IF $TALK_WOODSMAN = 0
		THEN
			SET TILE (12,3) = 0
		END
		#this is obsolete, leaving it in for now
		IF $TALK_WOODSMAN = 3
		THEN
			SET TILE (12,3) = 0
		END
		#this is obsolete, leaving it in for now
		IF $HAVE_TEDDY_BEAR = 1
		THEN
			SET TILE (12,3) = 0
		END			
	END
END

PRESS_FIRE AT SCREEN 0

	IF $TALK_MOTHER_DUNGEON_GATEMASTER = 99
	IF PLAYER_TOUCHES 7, 6
	THEN
		EXTERN 10
		#TALK TO MY DAUGHTER
		REDRAW
	END
	
	#TALK TO MY DAUGHTER
	IF $TALK_MOTHER_DUNGEON_GATEMASTER = 0
	IF PLAYER_TOUCHES 7, 6
	THEN
		EXTERN 12
		#TALK TO MY DAUGHTER
		REDRAW
	END
	
	IF $TALK_DUNGEON_GATEMASTER = 99
	IF PLAYER_TOUCHES 10, 2
	THEN
		EXTERN 10
		#HOW CAN I HELP YOU
		REDRAW
	END	
	
	IF $TALK_DUNGEON_GATEMASTER = 0
	IF PLAYER_TOUCHES 10, 2
	IF $TALK_WOODSMAN = 1
	THEN
		EXTERN 13
		#BOOHOO
		REDRAW		
	END
	
	#FIND MY TEDDYBEAR
	IF $TALK_DUNGEON_GATEMASTER = 0
	IF PLAYER_TOUCHES 10, 2
	IF $TALK_WOODSMAN = 0
	THEN
		SET $TALK_WOODSMAN = 1
		EXTERN 13
		#BOOHOO
		REDRAW
	END
	
	#FOUND TEDDYBEAR WILL NOW OPEN DUNGEON
	IF $TALK_DUNGEON_GATEMASTER = 1
	IF PLAYER_TOUCHES 10, 2
	THEN
		EXTERN 20
		#THANK YOU FOR GETTING TEDDYBEAR
		SET $DUNGEON_ONE_OPEN = 1
		PRINT_TILE_AT (0, 22) = 47
		#CLEARS TEDDY BEAR
		
		REDRAW
		
		SET $TALK_DUNGEON_GATEMASTER = 99
		SET $TALK_MOTHER_DUNGEON_GATEMASTER = 99
		#now they both ask if they can help you
	END	
	
	IF PLAYER_IN_X 97, 127
    IF PLAYER_IN_Y 129, 159
    THEN
		SET $WARPED_TO_HERE = 78
		WARP_TO_LEVEL 0, 4, 7, 7, 1
    END
END

#ISLAND KEEPER
ENTERING SCREEN 1
	IF TRUE
	THEN
		SET_FIRE_ZONE 97, 129, 127, 159
	END
END

PRESS_FIRE AT SCREEN 1	
IF $TALK_ISLAND_KEEPER < 2
	IF PLAYER_TOUCHES 8, 5
	THEN
		EXTERN 11
		#TEXT please help us stranger
		SET $TALK_ISLAND_KEEPER = 1
		REDRAW
	END
		
	IF $TALK_ISLAND_KEEPER_DAUGHTER = 0
	IF PLAYER_TOUCHES 2, 2
	THEN
		EXTERN 11
		#TEXT please help us stranger
		SET $TALK_ISLAND_KEEPER = 1
		REDRAW
	END
		
	#WARP TO LEVEL 0 IN FRONT OF GATEKEEPER HOUSE
	IF PLAYER_IN_X 97, 127
    IF PLAYER_IN_Y 129, 159
    THEN
		SET $WARPED_TO_HERE = 76
		WARP_TO_LEVEL 0, 10, 12, 7, 1
    END	
END

#woodsman
ENTERING SCREEN 2
IF TRUE
	THEN
		SET_FIRE_ZONE 97, 129, 127, 159		
	END
END

PRESS_FIRE AT SCREEN 2
	#NOTHING TO SAY
 	IF $TALK_WOODSMAN = 0 
	IF PLAYER_TOUCHES 9, 5
	THEN
		EXTERN 10 
		# HOW CAN I HELP YOU
		REDRAW
	END	
	
	#FIND THE AXE
	IF $TALK_WOODSMAN = 1
	IF PLAYER_TOUCHES 9, 5
	THEN
		SET $FOREST_GATE = 1
		EXTERN 15
		#I WILL HELP YOU WITH THE TEDDYBEAR
		REDRAW
	END
	
	#AXE IS FOUND BUT RUSTY
	IF $TALK_WOODSMAN = 2
	IF $FIREFLOWER_GATE = 0
	IF PLAYER_TOUCHES 9, 5
	THEN
		EXTERN 16
		#my axe is rusty
		REDRAW
		SET $TALK_BLACKSMITH = 1
	END
	
	IF $TALK_WOODSMAN = 3
	IF PLAYER_TOUCHES 9, 5
	THEN
		EXTERN 19
		# THANK YOU FOR FIXING MY AXE
		REDRAW
		SET $FIREFLOWER_GATE = 0
		PRINT_TILE_AT (0, 22) = 47
		#REMOVES AXE in bottom
		SET $TEDDYBEAR_GATE = 1
		SET $TALK_WOODSMAN = 0
	END
	
		
	IF PLAYER_IN_X 97, 127
    IF PLAYER_IN_Y 129, 159
    THEN
		SET $WARPED_TO_HERE = 80
		WARP_TO_LEVEL 0, 6, 7, 7, 1
    END
END

#gatekeeper /caveman
ENTERING SCREEN 3
IF TRUE
	THEN
		SET_FIRE_ZONE 97, 129, 127, 159
	END
END

#gatekeeper /caveman
PRESS_FIRE AT SCREEN 3

	IF $TALK_GATEKEEPER = 99
	IF PLAYER_TOUCHES 10, 5
	THEN
		EXTERN 10
		#YOU MUST CONQUER THE DUNGEON BEFORE YOU CAN GO NORTH.
		REDRAW
	END

	#YOU CANNOT PASS THE GATE
	IF $TALK_GATEKEEPER = 0
	IF PLAYER_TOUCHES 10, 5
	THEN
		EXTERN 14
		#YOU MUST CONQUER THE DUNGEON BEFORE YOU CAN GO NORTH.
		REDRAW
	END
	
	#you can now pass the gate
	IF $TALK_GATEKEEPER = 1
	IF PLAYER_TOUCHES 10, 5
	THEN
		EXTERN 21
		# YOU HAVE CONQURED THE DUNGEON
		
		PRINT_TILE_AT (0, 22) = 47
		#remove the sceptre from inventory
		
		REDRAW
		
		SET $SWAMP_GATE = 1
		SET $TALK_GATEKEEPER = 99
	END

	IF PLAYER_IN_X 97, 127
    IF PLAYER_IN_Y 129, 159
    THEN
		SET $WARPED_TO_HERE = 82
		WARP_TO_LEVEL 0, 1, 10, 1, 1
    END
END

#blacksmith
ENTERING SCREEN 4
IF TRUE
	THEN
		SET_FIRE_ZONE 97, 129, 127, 159
	END
END

#blacksmith
PRESS_FIRE AT SCREEN 4
	#NOTHING TO SAY
 	IF $TALK_BLACKSMITH = 0 
	IF PLAYER_TOUCHES 7, 5
	THEN
		EXTERN 10 
		#30 HOW CAN I HELP YOU
		REDRAW
	END
	
	IF $TALK_BLACKSMITH = 1
	IF PLAYER_TOUCHES 7, 5
	THEN
		EXTERN 17
		#I WILL FIX THE AXE, BUT I WILL NEED TO USE MY FORGE
		REDRAW
		SET $FIREFLOWER_GATE = 1
	END
	
	#FLOWER IS FOUND FIXING AXE
	IF $TALK_BLACKSMITH = 2
	IF PLAYER_TOUCHES 7, 5
	THEN
		PRINT_TILE_AT (2, 22) = 47
		#REMOVES FLOWER in bottom
		
		EXTERN 18
		#THANK YOU FOR THE FIRE FLOWER.
		REDRAW
		SET $TALK_WOODSMAN = 3
		SET $TALK_BLACKSMITH = 0
	END
	
	
	IF PLAYER_IN_X 97, 127
    IF PLAYER_IN_Y 129, 159
    THEN
		SET $WARPED_TO_HERE = 84
		WARP_TO_LEVEL 0, 8, 7, 4, 1
    END
END

#HUNGRY MAGICIAN
ENTERING SCREEN 5
IF TRUE
	THEN
		SET_FIRE_ZONE 109, 129, 115, 157
	END
END

#HUNGRY MAGICIAN
PRESS_FIRE AT SCREEN 5

	IF $HAVE_CRAB_CLAW = 1
	IF $HAVE_GIANT_CLAM = 1
	IF $HAVE_FROG = 1
	THEN
		SET $TALK_HUNGRY_MAGICIAN = 2
	END		
	
	IF $TALK_HUNGRY_MAGICIAN = 1
	IF PLAYER_TOUCHES 8, 6
	THEN
		EXTERN 23
		#YOU DO NOT HAVE ALL THREE ITEMS.
		
		REDRAW
	END
	
	IF $TALK_HUNGRY_MAGICIAN = 0
	IF PLAYER_TOUCHES 8, 6
	THEN
		SET $TALK_HUNGRY_MAGICIAN = 1
		EXTERN 22
		#MY NAME IS DOUG.
		REDRAW
	END
	
	
	
	IF $TALK_HUNGRY_MAGICIAN = 2
	IF PLAYER_TOUCHES 8, 6
	THEN
		EXTERN 24 
		# YUMM, DINNER!
		SET $MAP4_GATE = 1
		#OPENS GATE
		REDRAW
		
		#CLEAR INVENTORY
		PRINT_TILE_AT (0, 22) = 47
		#REMOVES FROG in bottom
		PRINT_TILE_AT (2, 22) = 47
		#REMOVES CRAB in bottom
		PRINT_TILE_AT (4, 22) = 47
		#REMOVES CLAM in bottom
		PRINT_TILE_AT (6, 22) = 47
		#REMOVES FROGSHIELD in bottom
		
		SET $TALK_HUNGRY_MAGICIAN = 3
		#TRANSPORTS TO START OF LEVEL 3
		SET $WARPED_TO_HERE = 24
		WARP_TO_LEVEL 3, 16, 3, 3, 1
	END
	
	IF $TALK_HUNGRY_MAGICIAN = 3
	IF PLAYER_TOUCHES 8, 6
	THEN
		EXTERN 27
		# I TOLD YOU TO PISS OFF!
		SET $MAP4_GATE = 1
		#OPENS GATE
		#SET $TALK_WIZARD = 0
		REDRAW
		
		#TRANSPORTS TO START OF LEVEL 3		
		SET $WARPED_TO_HERE = 30
		WARP_TO_LEVEL 3, 3, 1, 1, 1
		
	END
	
 	IF PLAYER_IN_X 109, 115
    IF PLAYER_IN_Y 129, 157
    THEN
		SET $WARPED_TO_HERE = 86
		WARP_TO_LEVEL 3, 11, 9, 5, 1
    END
END

#MAGICIAN COUSIN TAKES FROG
ENTERING SCREEN 6
IF TRUE
	THEN
		SET_FIRE_ZONE 97, 129, 127, 159
	END
END

#MAGICIAN COUSIN that ASS
PRESS_FIRE AT SCREEN 6
	
	#NOTHING TO SAY
 	IF $TALK_MAGICIAN_COUSIN = 0
	IF PLAYER_TOUCHES 10, 6
	THEN
		EXTERN 29
		# MY NAME IS ANTHONY.
		REDRAW
	END
	
	#TAKING FROG FOR KEY
	IF $TALK_MAGICIAN_COUSIN = 1 
	IF PLAYER_TOUCHES 10, 6
	THEN
		PRINT_TILE_AT (0, 22) = 47
		#REMOVE FROG in bottom
		
		SET $HAVE_FROG = 0
		EXTERN 25
		# IF YOU WANT THAT KEY,
		
		REDRAW
		SET $SWAMP_KEY_GATE = 1
		#REMOVE BLOCKS TO KEY
		
		SET $TALK_MAGICIAN_COUSIN = 2
		#TRANSPORTS TO START OF LEVEL 3
		
		SET $WARPED_TO_HERE = 24
		WARP_TO_LEVEL 3, 16, 3, 3, 1
	END
	
	IF $TALK_MAGICIAN_COUSIN = 2 
	IF PLAYER_TOUCHES 10, 6
	THEN
		PRINT_TILE_AT (0, 22) = 47
		#REMOVE FROG in bottom
		
		SET $HAVE_FROG = 0
		
		EXTERN 27
		# I TOLD YOU TO PISS OFF!
		REDRAW
		
		#TRANSPORTS TO START OF LEVEL 3
		SET $WARPED_TO_HERE = 24
		WARP_TO_LEVEL 3, 12, 6, 8, 1
	END
	
	
	IF PLAYER_IN_X 109, 115
    IF PLAYER_IN_Y 129, 157
    THEN
		SET $WARPED_TO_HERE = 88
		WARP_TO_LEVEL 3, 1, 9, 5, 1
    END	
END

#DESERT GATEMASTER
ENTERING SCREEN 7
IF TRUE
	THEN
		SET_FIRE_ZONE 97, 129, 127, 159
	END
END

PRESS_FIRE AT SCREEN 7

	IF $TALK_DESERT_GATEMASTER = 0
	IF PLAYER_TOUCHES 9, 5
	THEN
		EXTERN 31
		#YOU ARE PROBALLY LOOKING FOR A WAY TO CAPTURE A FROG
		
		EXTERN 41
		#I NEED A BOOK
		
		REDRAW
		
		SET $PLAINS_GATE = 1
		SET $TALK_BOOK_DEALER = 0
	END
	
	#gateway
	IF PLAYER_IN_X 97, 127
    IF PLAYER_IN_Y 129, 159
    THEN
		SET $WARPED_TO_HERE = 90
		WARP_TO_LEVEL 2, 5, 4, 5, 1
    END
	
	IF $TALK_DESERT_GATEMASTER = 1
	IF PLAYER_TOUCHES 9, 5
	THEN
		EXTERN 49
		REDRAW
	END
	
	IF $TALK_DESERT_GATEMASTER = 2
	IF PLAYER_TOUCHES 9, 5
	THEN
		EXTERN 50
		EXTERN 51
		PRINT_TILE_AT (10, 22) = 47
		#REMOVES BOOK in bottom
		
		#open dungeon
		SET $DUNGEON_TWO_OPEN = 1		
		REDRAW
	END

	IF $TALK_DESERT_GATEMASTER = 3
	IF PLAYER_TOUCHES 9, 5
	THEN
		EXTERN 52
		#SET $TALK_WIZARD = 0
		REDRAW
	END	
END

#BOOK DEALER
ENTERING SCREEN 8
IF TRUE
	THEN
		SET_FIRE_ZONE 97, 129, 127, 159
	END
END

PRESS_FIRE AT SCREEN 8

	IF $TALK_BOOK_DEALER = 0
	IF PLAYER_TOUCHES 4, 7
	THEN
		EXTERN 42
		#I WOULD BE HAPPY TO GET YOU THE BOOK BUT IT WILL COST YOU.
		
		EXTERN 43
		SET $TALK_BOOK_DEALER = 1
		SET $CAN_COLLECT_ORBS = 1
		SET $NUMBER_OF_ORBS = 0
		
		SET $TALK_DESERT_GATEMASTER = 1
		REDRAW		
	END
	
	#IF $NUMBER_OF_ORBS = 4
	IF $TALK_BOOK_DEALER = 1
	IF $HAVE_MAGENTA_ORB = 1
	IF $HAVE_BLUE_ORB = 1
	IF $HAVE_CYAN_ORB = 1
	IF $HAVE_RED_ORB = 1	
	IF PLAYER_TOUCHES 4, 7
	THEN
		EXTERN 47#YOU HAVE 4 ORBS
		
		PRINT_TILE_AT (10, 22) = 47
		#REMOVES ORB in bottom
		
		PRINT_TILE_AT (12, 22) = 47
		#REMOVES ORB in bottom
		
		PRINT_TILE_AT (14, 22) = 47
		#REMOVES ORB in bottom
		
		PRINT_TILE_AT (16, 22) = 47
		#REMOVES ORB in bottom
		
		EXTERN 48
		#evil plan revealed
		#baddies alway do this
		
		PRINT_TILE_AT (10, 22) = 41
		#shows BOOK in bottom
		
		REDRAW
		
		SET $TALK_BOOK_DEALER = 2
		#GIVE BOOK
		
		SET $TALK_DESERT_GATEMASTER = 2
		
		SET $WARPED_TO_HERE = 91
		WARP_TO_LEVEL 5, 11, 7, 5, 1		
	END
	
	
	IF PLAYER_TOUCHES 4, 7
	IF $TALK_BOOK_DEALER = 1
	IF $NUMBER_OF_ORBS <> 4	
	THEN
		EXTERN 44
		#YOU DO NOT HAVE THE 4 ORBS YET
		REDRAW
	END
	
 	#gateway
	IF PLAYER_IN_X 97, 127
    IF PLAYER_IN_Y 129, 159
    THEN
		SET $TALK_BOOK_DEALER = 1
		SET $WARPED_TO_HERE = 90
		WARP_TO_LEVEL 5, 7, 7, 5, 1
    END	
END

#ISLAND WIZARD
ENTERING SCREEN 9
IF TRUE
	THEN
		SET_FIRE_ZONE 97, 129, 127, 159
		EXTERN 246 
		#LIGHTS ON
		
		SET $DUNGEON_FOUR_CONQUERED = 1
		
		IF $NUMBER_STAFF_HALVES = 0
		THEN
			SET $TALK_WIZARD = 0
		END
		
		IF $NUMBER_STAFF_HALVES = 1
		THEN
			SET $TALK_WIZARD = 1
		END
		
		IF $NUMBER_STAFF_HALVES = 2
		THEN
			SET $TALK_WIZARD = 2
		END
		
		#THERE ARE NOT 3 HALVES OF THE STAFF
		# WE ARE USING THIS FLAG TO SHOW NEXT DIALOG
		IF $NUMBER_STAFF_HALVES = 3
		THEN
			SET $TALK_WIZARD = 3
		END
		
	END
END

PRESS_FIRE AT SCREEN 9

	IF $TALK_WIZARD = 0
	IF PLAYER_TOUCHES 3, 5
	THEN
		EXTERN 53
		EXTERN 54
		EXTERN 55
		SET $ALL_GATES_OPEN = 0
		SET $CASTLE_LOCKED_ROOM2 = 1
		SET $SWITCH_4 = 1
		
		SET $DUNGEON_FOUR_CONQUERED = 1
		REDRAW
	END	

	IF $TALK_WIZARD = 1
	IF PLAYER_TOUCHES 3, 5
	THEN
		SET $SWITCH_4 = 1
		EXTERN 57
		REDRAW
	END

	IF $TALK_WIZARD = 2
	IF PLAYER_TOUCHES 3, 5
	THEN
		#NOW WE HAVE BOTH HALVES
		SET $SWITCH_4 = 1
		EXTERN 58
		SET $NUMBER_STAFF_HALVES = 3
		#USED TO PUSH THE DIALOG ALONG
		
		SET $ALL_GATES_OPEN = 1
		PRINT_TILE_AT (0, 22) = 47 
		#remove the staff from inventory
		PRINT_TILE_AT (2, 22) = 47
		#remove the staff from inventory
		REDRAW
	END


 	#gateway
	IF PLAYER_IN_X 97, 127
    IF PLAYER_IN_Y 129, 159
    THEN
		SET $WARPED_TO_HERE = 25
		WARP_TO_LEVEL 4, 5, 13, 1, 1
    END	
END


#BOOM BOOM BETTY
ENTERING SCREEN 10
IF TRUE
	THEN
		SET_FIRE_ZONE 97, 129, 127, 159		
	END
END

PRESS_FIRE AT SCREEN 10	
	
	IF $TALK_BOOM_BETTY = 0
	IF PLAYER_TOUCHES 9, 5
	THEN
		EXTERN 10 
		# HOW CAN I HELP YOU
		REDRAW
	END
	
	IF $TALK_BOOM_BETTY = 1
	IF $NEED_DYNAMITE = 0
	IF PLAYER_TOUCHES 9, 5
	THEN
		EXTERN 66
		#DYNAMITE QUEST
		EXTERN 67
		#DYNAMITE QUEST
		SET $NEED_DYNAMITE = 0
		#NOW WE ARE USING THIS VARIABLE TO COUNT STICKS OF DYNAMITE
		SET $TALK_BOOM_BETTY = 2
		REDRAW
	END
	
	
	IF $TALK_BOOM_BETTY = 2
	IF $NEED_DYNAMITE < 7
	IF PLAYER_TOUCHES 9, 5
	THEN
		EXTERN 68
		#I NEED 7 STICKS
		REDRAW
	END
	
	IF $TALK_BOOM_BETTY = 2
	IF $NEED_DYNAMITE = 7
	IF PLAYER_TOUCHES 9, 5
	THEN
		EXTERN 69
		#I WILL CLEAR A PATH

		PRINT_TILE_AT (2, 22) = 47
		PRINT_TILE_AT (4, 22) = 47
		PRINT_TILE_AT (6, 22) = 47
		PRINT_TILE_AT (8, 22) = 47
		PRINT_TILE_AT (10, 22) = 47
		PRINT_TILE_AT (12, 22) = 47
		PRINT_TILE_AT (14, 22) = 47
		
		#NEED TO TRIGGER A FLAG TO CLEAR THE ROCKS
		SET $ROCK_SLIDE = 1
		REDRAW
	END
	
	
	#gateway
	IF PLAYER_IN_X 97, 127
    IF PLAYER_IN_Y 129, 159
    THEN
		SET $WARPED_TO_HERE = 84
		WARP_TO_LEVEL 7, 7, 6, 3, 1
    END	
END

#final screen with librarian
ENTERING SCREEN 12
	IF TRUE
	THEN	
		MUSIC 12
		#fighting music
		
		SET_FIRE_ZONE 201, 121, 215, 135
		
		#DISPLAYS GOLDEN APPLE OR NOT
		IF $HAVE_GOLDEN_APPLE = 0
		THEN
			SET TILE (2,2) = 38
		END		
		IF $HAVE_GOLDEN_APPLE = 1
		THEN
			SET TILE (2,2) = 0
		END		
		
		#here is our last puzzle
		IF $SWITCH_10 = 0
		THEN
		#MOVE SWITCH
			SET TILE (1, 7) = 39
			#LOWER SPIKES
			SET TILE (5,5) = 2
			SET TILE (6,5) = 2
			SET TILE (7,5) = 2
			SET TILE (8,5) = 2
			SET TILE (9,5) = 2
		END
	
		IF $SWITCH_10 = 1
		THEN
			#MOVE SWITCH
			SET TILE (1, 7) = 40
			
			#RAISE SPIKES
			SET TILE (5,5) = 33
			SET TILE (6,5) = 33
			SET TILE (7,5) = 33
			SET TILE (8,5) = 33
			SET TILE (9,5) = 33
		END
		
		IF $SWITCH_11 = 0
		THEN
		#MOVE SWITCH
			SET TILE (6, 2) = 39
			
			#LOWER SPIKES
			SET TILE (10,1) = 2
			SET TILE (10,2) = 2
			SET TILE (10,3) = 2
			SET TILE (10,4) = 2
			SET TILE (10,5) = 2
		END
		
		IF $SWITCH_11 = 1
		THEN
			#MOVE SWITCH
			SET TILE (6, 2) = 40
			
			#RAISE SPIKES
			SET TILE (10,1) = 33
			SET TILE (10,2) = 33
			SET TILE (10,3) = 33
			SET TILE (10,4) = 33
			SET TILE (10,5) = 33
		END	
		
		IF $SWITCH_12 = 0
		THEN
			#MOVE SWITCH
			SET TILE (6, 7) = 39
			
			SET $SWITCH_1 = 0
			#SWITCH 1 WHICH IS PLACED LAST IS 
			#USED TO TURN LIGHTS ON AND OFF
		END
	
		IF $SWITCH_12 = 1
		THEN
			#MOVE SWITCH
			SET TILE (6, 7) = 40
			
			SET $SWITCH_1 = 1
			#SWITCH 1 WHICH IS PLACED LAST IS 
			#USED TO TURN LIGHTS ON AND OFF
		END	
		
		IF $SWITCH_13 = 0
		THEN
		#MOVE SWITCH
			SET TILE (13, 2) = 39
		END
		
		IF $SWITCH_13 = 1
		THEN
			#MOVE SWITCH
			SET TILE (13, 2) = 40
			ENEMY 0 ON
		END	
		
		IF $SWITCH_14 = 0
		THEN
		#MOVE SWITCH
			SET TILE (13, 5) = 39
		END
		
		IF $SWITCH_14 = 1
		THEN
			#MOVE SWITCH
			SET TILE (13, 5) = 40			
		END
		
		
		#USED TO TURN LIGHTS ON AND OFF
		IF $SWITCH_1 = 0
		THEN
			EXTERN 246 
			REDRAW
			#LIGHTS ON
		END
		
		IF $SWITCH_1 = 1
		THEN
			EXTERN 247 
			#LIGHTS OFF
		END
		
		#OPEN DOOR
		IF $SWITCH_10 = 1
		IF $SWITCH_11 = 1
		IF $SWITCH_12 = 1
		IF $SWITCH_13 = 1
		THEN
			SET TILE (1,4) = 2
		END
				
	END
END

PRESS_FIRE AT SCREEN 12	

	IF $SWITCH_8 = 0
	THEN
		SET $SWITCH_8 = 1
		SET_FIRE_ZONE 0,0,0,0 
		EXTERN 72
		REDRAW
	END
		
	
	###########
	#SWITCHES LOGIC
	###########
	# SWITCH_10
	###########
	# EVIL TRICK IS TO RAISE SPIKES
	IF PLAYER_TOUCHES 1,7
	THEN
		#base switch
		FLIPFLOP $SWITCH_10	

		#MESS WITH THE OTHER SWITCHES
		
		#EVIL TRICK IS TO TURN THE LIGHTS ON AND OFF
		FLIPFLOP $SWITCH_12
		
		REENTER		
	END	
	
	# SWITCH_11
	#EVIL TRICK IS TO RAISE SPIKES	
	IF PLAYER_TOUCHES 6,2
	THEN
		#base switch
		FLIPFLOP $SWITCH_11
		
		#MESS WITH THE OTHER SWITCHES
		#EVIL TRICK IS TO RAISE SPIKES
		FLIPFLOP $SWITCH_10	
		
		FLIPFLOP $SWITCH_12
		REENTER
	END	
	
	# SWITCH_12
	#EVIL TRICK IS TO TURN THE LIGHTS ON AND OFF
	IF PLAYER_TOUCHES 6,7
	THEN
		#base switch
		FLIPFLOP $SWITCH_12
		
		FLIPFLOP $SWITCH_11
		
	END
	
	# SWITCH_13
	#EVIL TRICK IS TO RESURRECT BOSS
	IF PLAYER_TOUCHES 13,2
	THEN
		#base switch
		FLIPFLOP $SWITCH_13
		
		#MESS WITH THE OTHER SWITCHES
		#EVIL TRICK IS TO RAISE SPIKES
		FLIPFLOP $SWITCH_10		
		ENEMY 0 ON			
		REENTER
	END
	
	# SWITCH_14
	#EVIL TRICK IS TO TELEPORT YOU TO ANOTHER PART OF THE SCREEN
	IF PLAYER_TOUCHES 13,5
	THEN
		#SO THAT WARNING WILL ONLY SHOW ONCE
		#YOU WILL NOT LEAVE HERE ALIVE
		SET $SWITCH_8 = 1
		FLIPFLOP $SWITCH_14
		#base switch
	END
		
	######################
	#DRAW SWITCH POSITIONS
	######################
	
	###########
	#SWITCH 10
	###########
	IF $SWITCH_10 = 0
	IF PLAYER_TOUCHES 1,7
	THEN
	#MOVE SWITCH
		SET TILE (1, 7) = 39
	END	
	
	IF $SWITCH_10 = 1
	IF PLAYER_TOUCHES 1,7
	THEN
		#MOVE SWITCH
		SET TILE (1, 7) = 40
	END
	
	###########
	#SWITCH 11
	###########
	IF $SWITCH_11 = 0
	IF PLAYER_TOUCHES 6,2
	THEN
	#MOVE SWITCH
		SET TILE (6, 2) = 39
	END	
	
	IF $SWITCH_11 = 1
	IF PLAYER_TOUCHES 6,2
	THEN
		#MOVE SWITCH
		SET TILE (6, 2) = 40
	END	

	
	###########
	#SWITCH 12
	###########	
	#HAVE TO PLACE IN AN EXTRA VARIABLE CHECK BECAUSE OTHERWISE CONSTANT REDRAW WILL SLOW DOWN GAME
	IF $SWITCH_12 = 0
	IF PLAYER_TOUCHES 6,7
	THEN
	#MOVE SWITCH
		SET TILE (6, 7) = 39
#		EXTERN 246 #LIGHTS ON
#		REDRAW
#		#THIS WILL CAUSE DELAY ISSUES WHEN CONSTANTLY CALLED
		REENTER
	END
	
	IF $SWITCH_12 = 1
	IF PLAYER_TOUCHES 6,7
	THEN
		#MOVE SWITCH
		SET TILE (6, 7) = 40
		EXTERN 247 #LIGHTS OFF
		REDRAW
		REENTER
	END	
	

	###########
	#SWITCH 13
	###########	
	IF $SWITCH_13 = 0
	THEN
		#MOVE SWITCH
		SET TILE (13, 2) = 39
	END
	
	IF $SWITCH_13 = 1
	THEN
		#MOVE SWITCH
		SET TILE (13, 2) = 40
		ENEMY 0 ON
	END	
	

	###########
	#SWITCH 14
	###########	
	#HAVE TO PLACE IN AN EXTRA VARIABLE CHECK BECAUSE OTHERWISE CONSTANT REDRAW WILL SLOW DOWN GAME
	IF $SWITCH_14 = 0
	THEN
	#MOVE SWITCH
		SET TILE (13, 5) = 39
	END
	
	#HAVE TO PLACE IN AN EXTRA VARIABLE CHECK BECAUSE OTHERWISE CONSTANT REDRAW WILL SLOW DOWN GAME
	IF $SWITCH_14 = 1
	THEN
		#MOVE SWITCH
		SET TILE (13, 5) = 40
		SET $SWITCH_8 = 1
		# REPOSTN 13, 8
	END	
	
	###########
	#DRAW SWITCH POSITIONS
	###########
	
	
	IF $SWITCH_10 = 1
	IF $SWITCH_11 = 1
	IF $SWITCH_12 = 1
	IF $SWITCH_13 = 1
	THEN
		SET TILE (1,4) = 2
		ENEMY 0 ON
	END
	
	#SOLUTION 1 2 3 4 1 2
	#GET GOLDEN APPLE

	IF PLAYER_TOUCHES 2, 2
	IF $HAVE_GOLDEN_APPLE = 0
	THEN
				
		#TURN ON THE LIGHTS TO COLLECT APPLE AND DISPLAY TEST
		EXTERN 246 
		REDRAW				
				
		EXTERN 73 #TEXT YOU HAVE FOUND THE APPLE
		RECHARGE #RECHARGE YOUR LIFE
		SET TILE (2,2) = 0
		PRINT_TILE_AT (2, 22) = 38
		SET $HAVE_GOLDEN_APPLE = 1
		#CLEARS THE TEXT
		REDRAW
		
		
		SET $RAISE_LANDBRIDGE = 1
		
		SET $WARPED_TO_HERE = 99
		WARP_TO_LEVEL 2, 3, 1, 8, 1
	END	
END


END_OF_LEVEL
#end level 9

